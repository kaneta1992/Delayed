<!DOCTYPE html>
<head>
<meta charset="utf-8"/>
</head>

<script id="extractFar" type="x-shader/x-fragment">
#version 300 es
precision highp float;
uniform vec2  resolution;
uniform sampler2D tex, depthNormalTexture;
out vec4 fragColor;

void main( )
{
    vec2 uv = gl_FragCoord.xy / resolution;
    fragColor = mix(textureLod(tex, uv, 0.0), vec4(0.0), float(textureLod(depthNormalTexture, uv, 0.0).w < 15.0));
}
</script>

<script id="extractNear" type="x-shader/x-fragment">
#version 300 es
precision highp float;
uniform vec2  resolution;
uniform sampler2D tex, depthNormalTexture;
out vec4 fragColor;

void main( )
{
    vec2 uv = gl_FragCoord.xy / resolution;
    fragColor = mix(textureLod(tex, uv, 0.0), vec4(0.0), float(textureLod(depthNormalTexture, uv, 0.0).w > 1.0));
}
</script>

<script id="DOF" type="x-shader/x-fragment">
#version 300 es
precision highp float;
uniform vec2  resolution;
uniform sampler2D tex;
out vec4 fragColor;

void main( )
{
    vec2 uv = gl_FragCoord.xy / resolution;
    vec3 sum = vec3(0.);
    float sumW = 0.0;
    for(int y=-6;y<=6;++y) {
        for(int x=-6;x<=6;++x) {
            if(x*x+y*y<37) {
                vec4 col = textureLod(tex, uv + vec2(x,y)/resolution, 0.0);
                float w = max(col.w, 0.0001);
                sum += col.rgb * w;
                sumW += w;
            }
        }
    }
    fragColor = vec4(sum / sumW, 1.0);
}
</script>

<script id="DOFCombine" type="x-shader/x-fragment">
#version 300 es
precision highp float;
uniform vec2  resolution;
uniform sampler2D tex, depthNormalTexture, DOFNearTexture, DOFFarTexture;
out vec4 fragColor;

void main( )
{
    vec2 uv = gl_FragCoord.xy / resolution;
    float depth = textureLod(depthNormalTexture, uv, 0.0).w;
    vec4 baseCol = textureLod(tex, uv, 0.0);
    baseCol = mix(baseCol, textureLod(DOFNearTexture, uv, 0.0), pow(clamp((1.0 - depth), 0.0, 1.0), 1.0));
    fragColor = mix(baseCol, textureLod(DOFFarTexture, uv, 0.0), pow(clamp((depth - 15.0) * 0.05, 0.0, 1.0), 2.0));
}
</script>

<script id="gaussian" type="x-shader/x-fragment">
#version 300 es
precision highp float;
uniform vec2  resolution, blurDir;
uniform sampler2D tex; 
out vec4 fragColor;

void main( )
{
    vec2 uv = gl_FragCoord.xy / resolution, dir = blurDir / resolution;
    vec4 sum = vec4(0.0);
    for (int i = -5; i <= 5; i++) {
        sum += float[](0.200565,0.176998,0.121649,0.0651141,0.0271436,0.00881223)[abs(i)] * textureLod(tex, uv + float(i) * dir, 0.0);
    }
    fragColor=sum;
}
</script>

<script id="blurReflection" type="x-shader/x-fragment">
#version 300 es
precision highp float;
uniform vec2  resolution, blurDir;
uniform sampler2D tex, depthNormalTexture, roughnessTexture;
out vec4 fragColor;

void main( )
{
    vec2 uv = gl_FragCoord.xy / resolution, dir = blurDir / resolution;
    vec3 sum = vec3(0.0);
    float wSum = 0.0;
    vec4 centerDepthNormal = textureLod(depthNormalTexture, uv, 0.0);
    float roughness = textureLod(roughnessTexture, uv, 0.0).w;
    for (int i = -20; i <= 20; i++) {
        vec2 offset = float(i) * dir*(roughness * 8.0);
        vec4 tapDepthNormal = textureLod(depthNormalTexture, uv + offset, 0.0);
        float nw = max(0.001, pow(abs(dot(tapDepthNormal.xyz, centerDepthNormal.xyz)), 8.0));
        float r2 = (tapDepthNormal.w - centerDepthNormal.w) * 1.0;
        // https://github.com/keijiro/AdamPlaneReflection/blob/master/Assets/AdamPlaneReflection/PlaneReflection/Convolve.shader#L92-L93
        float w = max(0.f, cos(clamp(float(i) / 20., -1.57, 1.57))) * nw * exp(-r2*r2);
        wSum += w;
        sum += w * textureLod(tex, uv + offset, 0.0).rgb;
    }
    fragColor=vec4(sum / wSum, 1.0);
}
</script>

<script id="sumBloom" type="x-shader/x-fragment">
#version 300 es
precision highp float;
uniform vec2  resolution;
uniform sampler2D tex1, tex2, tex3, tex4, tex5;
out vec4 fragColor;

void main( )
{
    vec2 uv = (gl_FragCoord.xy) / resolution;
    float intensity = 0.015;
    vec3 sum = textureLod(tex1, uv, 0.0).rgb * intensity * 1.0;
    sum += textureLod(tex2, uv, 0.0).rgb * intensity * 2.0;
    sum += textureLod(tex3, uv, 0.0).rgb * intensity * 4.0;
    sum += textureLod(tex4, uv, 0.0).rgb * intensity * 8.0;
    sum += textureLod(tex5, uv, 0.0).rgb * intensity * 16.0;
    fragColor=vec4(sum, 1.0);
}
</script>

<script id="mainHeader" type="x-shader/x-fragment">
#version 300 es
precision highp float;
uniform vec2  resolution; 
uniform float time;
uniform sampler2D reflectTexture, depthNormalTexture, roughnessTexture, baseColorTexture;

layout (location = 0) out vec4 fragColor;
layout (location = 1) out vec4 fragColor2;
layout (location = 2) out vec4 fragColor3;
layout (location = 3) out vec4 fragColor4;

#define MAT_INSIDE  0.
#define MAT_OUTSIDE 1.
#define MAT_PILLAR  2.
#define MAT_BOARD   3.
#define MAT_LIGHT   4.
#define MAT_GOLD   5.
#define MAT_BLACK   6.
#define MAT_SPHERE   7.

float tt;

float sphere(vec3 p, float s) {
    return length(p) - s;
}

float rBox( vec3 p, vec3 b, float r ){
    vec3 q = abs(p) - b;
    return length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0) - r;
}

float cylinder(vec3 p, float r, float h) {
    float d = length(p.xz) - r;
    float clip = abs(p.y) - h;
    return max(d, clip);
}

vec3 boardOffset;
float boardDistance(vec3 p, vec3 offset) {
    boardOffset = offset;
    p -= offset;
    vec3 connectionPos = p;
    connectionPos.x = abs(connectionPos.x) - 0.5;
    connectionPos.y -= 2.0;
    
    float d = cylinder(connectionPos, 0.025, 2.0);
    d = min(d, rBox(p, vec3(0.8, 0.3, 0.05), 0.05));
    return d;
}

float lightsDistance(vec3 p) {
    p.z = mod(p.z + 3.0, 6.0) - 3.0;
    p.x = abs(p.x) - 1.0;
    float d = rBox(p, vec3(0.1, 0.001, 1.5), 0.02);
    return d;
}

vec2 min2(vec2 a, vec2 b) {
    return a.x < b.x ? a : b;
}

vec2 max2(vec2 a, vec2 b) {
    return a.x > b.x ? a : b;
}

float hash( float n )
{
    return fract(sin(n)*43758.5453);
}

float noise( in vec3 x )
{
    vec3 p = floor(x);
    vec3 f = fract(x);

    f = f*f*(3.0-2.0*f);

    float n = p.x + p.y*57.0 + 113.0*p.z;

    float res = mix(mix(mix( hash(n+  0.0), hash(n+  1.0),f.x),
                        mix( hash(n+ 57.0), hash(n+ 58.0),f.x),f.y),
                    mix(mix( hash(n+113.0), hash(n+114.0),f.x),
                        mix( hash(n+170.0), hash(n+171.0),f.x),f.y),f.z);
    return res;
}

#define repid(p, r) (floor((p + r*.5) / r))
#define rep(p, r) (mod(p + r*.5, r) - r*.5)

mat2 rotate(float a) {
	float s = sin(a), c = cos(a);
    return mat2(c, s, -s, c);
}

float boxes(vec3 p, float noiseOffset, float interval, float clipNum, float start, float width, float smo, float t) {
    vec3 modp = rep(p, interval);
    vec3 id = repid(p, interval);
    
    float niz = noise(id + noiseOffset);
    float niz2 = noise(id + noiseOffset + 200.) * 0.7 + 0.3;
    
	float leftTop = start;
    float rightTop = start + width;
    float size = smoothstep(leftTop - smo, leftTop, t) - smoothstep(rightTop, rightTop + smo, t);
    
    vec3 boxp = modp;
    boxp.xy *= rotate(niz2 + tt * niz2);
    boxp.xz *= rotate(niz2 + tt * niz2);
    float d = rBox(boxp, vec3(0.15) * size * niz2, 0.03 * size * niz2);
    
    p.z = t;
    vec3 clipp = abs(p) - vec3(vec2((interval * clipNum) * 0.5), width + smo);
    float clipd = max(clipp.x, max(clipp.y, clipp.z));
    
    if (niz < 0.85 || size <= 0.001) {
        vec3 latice = abs(modp) - interval*0.5;
    	d = -max(latice.x, max(latice.y, latice.z))+0.2;
    }
    return max(d*0.6, clipd);
}

const float pi = acos(-1.);
const float pi2 = pi*2.;

float spaghetti(vec3 p)
{
    
    float b = 99999.;
    
    const int ite = 4;
    vec3 q = p;
   
    for(int i=0; i<ite; i++) {
        p = q;
        //float rnd = 1.0 + hash(float(i * i * i));
        float rnd2 = 1.0 + hash(float(i * i * i) + 1000.0);
        float stateTime = p.z * rnd2 * 0.2 + float(i) * rnd2;
    	p.xy *= rotate(stateTime + rnd2 * pi2);
        p.y = abs(p.y - sin(stateTime*0.1) * 2.0* rnd2);
        p = abs(p) - 1.0;
    	p.xy = p.xy * rotate(stateTime + rnd2 * pi2) + vec2(0.5, 0.3 + float(i) / float(ite)) * 1. * rnd2;
        
        b = min(b, rBox(p - vec3(0.0, 0.0, tt * 0.5), vec3(0.05, 0.05, rnd2 * 1. + tt * 0.5 + 2.0 ), 0.01));
    }
    return b * .6;
}


vec2 map(vec3 p) {
    vec2 floorPos = p.xy;
    floorPos = abs(floorPos) - vec2(5.0, 1.5);
    vec2 flor = vec2(-max(floorPos.x, floorPos.y), MAT_INSIDE);
    
    vec3 pillarPos = p;
    pillarPos.x = abs(pillarPos.x) - 2.5;
    pillarPos.z = mod(pillarPos.z + 2.5, 5.0) - 2.5;
    vec2 pillar = vec2(rBox(pillarPos, vec3(0.2, 4.0, 0.2), 0.05), MAT_PILLAR);
    
    vec2 substractBoxPos = p.xy;
    substractBoxPos.x = abs(substractBoxPos.x) - 7.0;
    substractBoxPos = abs(substractBoxPos) - vec2(3.0, 4.0);
    vec2 substractBox = vec2(-max(substractBoxPos.x, substractBoxPos.y), MAT_OUTSIDE);
    
    vec2 d = min2(pillar, flor);
    d = max2(d, substractBox);
    
    vec3 boardPos = p;
    
    boardPos.z = mod(boardPos.z + 10.0, 20.0) - 10.0;
    vec2 board = vec2(boardDistance(boardPos, vec3(0.0, 1.0, 0.0)), MAT_BOARD);
    d = min2(d, board);

    vec3 spherePos = vec3(0.0);
    float size = 0.0;
    if (time > 9.0 && time < 17.0) {
        spherePos = vec3(0.0, -0.5, 18.0);
        size = 0.8;
    } else if(time > 17.0 && time < 49.0) {
        spherePos = vec3(0.0, -0.5, tt + 4.0);
        size = 0.3;
    } else if(time > 49.0) {
        spherePos = vec3(0.0, -0.5, -16.0);
        size = 0.8;
    }

    d = min2(d, vec2(sphere(p - spherePos, size), MAT_SPHERE));

    if (time > 17.0 && time < 47.75) {
        d = min2(d, vec2(boxes(p, 0.0, 0.6, 3.0, 0.0, 1.0, 4.0, p.z - tt), MAT_GOLD));
        d = min2(d, vec2(boxes(p - vec3(0., 0.0, 1.0), 100.0, 1.0, 5.0, 0.0, 1.0, 4.0, p.z - tt), MAT_GOLD));
    }
    if (time > 33.0 && time < 48.0) {
        d = min2(d, vec2(spaghetti(p), MAT_BLACK));
    }

    p.y -= 1.5;
    vec2 lights = vec2(lightsDistance(p), MAT_LIGHT);
    d = min2(d, lights);

    return d;
}

vec3 hash(vec2 p)
{
	vec3 p3 = fract(vec3(p.xyx) * vec3(.1031, .1030, .0973));
    p3 += dot(p3, p3.yxz+33.33);
    return fract((p3.xxy+p3.yzz)*p3.zyx);
}

vec3 noised( in vec2 x )
{
    vec2 p = floor(x);
    vec2 f = fract(x);
    vec2 u = f*f*(3.0-2.0*f);
	float a = hash(p+vec2(0.5,0.5)).x;
	float b = hash(p+vec2(1.5,0.5)).x;
	float c = hash(p+vec2(0.5,1.5)).x;
	float d = hash(p+vec2(1.5,1.5)).x;
	return vec3(a+(b-a)*u.x+(c-a)*u.y+(a-b-c+d)*u.x*u.y,
				6.0*f*(1.0-f)*(vec2(b-a,c-a)+(a-b-c+d)*u.yx));
}

const int NOISE_OCTAVES = 7;

float elevated(vec2 p)
{
	const mat2 m2 = mat2(0.8,-0.6,0.6,0.8);
    
    p = p*3.0+vec2(10.0,-1.0);
    
    float a = 0.0;
    float b = 1.0;
	vec2  d = vec2(0.0);
    for( int i=0; i<NOISE_OCTAVES; i++ )
    {
        vec3 n = noised(p);
        d += n.yz;
        a += b*n.x/(1.0+dot(d,d));
		b *= 0.5;
        p = m2*p*2.0;
    }
    return a;
}

float tileOffset(vec2 p) {
    float pattern = elevated(p*0.5);
    return pattern;
}

vec3 normal3( in vec3 pos, float eps )
{
    vec2 e = vec2(1.0,-1.0)*0.5773*eps;
    return normalize( e.xyy*(map( pos + e.xyy ).x) +
					  e.yyx*(map( pos + e.yyx ).x) +
					  e.yxy*(map( pos + e.yxy ).x) +
					  e.xxx*(map( pos + e.xxx ).x) );
}

void ceilProperty(vec3 p, vec2 mat, out vec3 baseColor, out float roughness, out float metallic) {
    vec3 q = p;
    p.z = mod(p.z + 0.15, 0.3) - 0.15;
    p.z = abs(p.z);
    float t = smoothstep(0.001, 0.015, p.z);
    baseColor = vec3(mix(vec3(0.49), vec3(0.6), t));
    roughness = mix(0.6, pow(tileOffset(q.xz*0.5), 2.0) * 0.3, smoothstep(0.001, 0.015, p.z));
}

void floorProperty(vec3 p, vec2 mat, out vec3 baseColor, out float roughness, out float metallic) {
    vec2 latticePos = p.xz;
    latticePos = mod(latticePos + 0.15, 0.3) - 0.15;
    latticePos = abs(latticePos);
    float latticeAlpha = smoothstep(0.001, 0.015, min(latticePos.x, latticePos.y));
    
    vec2 blockPos = p.xz;
    blockPos.x = abs(blockPos.x) - 3.5; // block interval
    blockPos.x = abs(blockPos.x) - 0.2; // block width
    float blockAlpha = smoothstep(0.006, 0.001, blockPos.x);
    
    
    baseColor = vec3(mix(vec3(0.4), vec3(0.6), latticeAlpha));
    baseColor = vec3(mix(baseColor, vec3(0.6, 0.5, 0.1), blockAlpha));

    roughness = mix(0.8, pow(tileOffset(p.xz*2.), 2.0) * 0.7, smoothstep(0.001, 0.03, min(latticePos.x, latticePos.y)));
    roughness = mix(roughness, 1.0, blockAlpha);
    roughness = clamp(roughness, 0.0, 1.0);
}

float t3(vec3 p, vec3 n)
{
 	return  (tileOffset(p.xy)*n.z*n.z
            +tileOffset(p.zy)*n.x*n.x
            +tileOffset(p.xz)*n.y*n.y);
}

void pillarProperty(vec3 p, vec3 n, vec2 mat, out vec3 baseColor, out float roughness, out float metallic) {
    vec3 pillarPos = p;
    pillarPos.y = mod(pillarPos.y + 0.4, 0.8) - 0.4;
    float d = abs(pillarPos.y);
    float pillarAlpha = smoothstep(0.001, 0.006, d);

    baseColor = vec3(mix(vec3(0.2), vec3(0.55), pillarAlpha));
    roughness = pow(abs(p.y) - 0.5, 2.0) * 0.4;
    roughness += pow(t3(p*vec3(8.0, 2.0, 8.0), n), 1.0) * 0.3 + 0.1;
    roughness = mix(0.8, roughness, pillarAlpha);
    roughness = clamp(roughness, 0.0, 1.0);
}

float pattern(vec2 uv) {
    float t = 0.0;
    for(int i=0;i<=int(mod((time-1.)*4., 32.0));i++)t += float(0x84B0849>>i&1);
    float h2 = hash(t+10.0)*2.0-1.0, h3 = hash(t+4000.0)*2.0-1.0, h4 = hash(t+1000.);
	uv = floor(uv * vec2(h3*0.05,h2*0.05));
    return min(1.0, mod(uv.x + uv.y, 2.0 + floor(h4*8.0)));
}

void outsideProperty(vec3 p, vec3 n, vec2 mat, out vec3 baseColor, out float roughness, out float metallic) {
    float intensity = smoothstep(15.0, 17.0, time) * 4000.0 + 1.0;
    if (time > 17.0) intensity*=pattern(p.yz*20.0);
    if (time > 33.0) intensity*=pattern(p.yz*60.0);
    baseColor = vec3(0.05, 0.02, 0.005)*intensity;
    roughness = pow(t3(p*vec3(2.0, 2.0, 2.0), n), 1.0) * 0.6 + 0.1;
}

void boardProperty(vec3 p, vec2 mat, out vec3 baseColor, out float roughness, out float metallic) {
    p -= boardOffset;
    vec2 displayPos = p.xy;
    displayPos.y = abs(displayPos.y + 0.08) - 0.1;
    displayPos = abs(displayPos) - vec2(0.7, 0.08);
    
    float d = max(displayPos.x, displayPos.y);
    float displayAlpha = smoothstep(0.006, 0.001, d);
    
    baseColor = mix(vec3(0.02), vec3(0.005), displayAlpha);
    roughness = mix(0.4, 0.0, displayAlpha);
}

void materialToProperty(vec3 p, vec2 mat, out vec3 baseColor, out vec3 normalVec, out float roughness, out float metallic) {
    baseColor = vec3(0.0);
    roughness = 0.0;
    metallic = 0.0;
    normalVec = normal3(p, 0.0005);
    if (mat.y == MAT_INSIDE) {
        if (p.y < 0.0) {
            // floor
            floorProperty(p, mat, baseColor, roughness, metallic);
        } else {
            // ceil
            ceilProperty(p, mat, baseColor, roughness, metallic);
        }
    } else if (mat.y == MAT_OUTSIDE) {
        outsideProperty(p, normalVec, mat, baseColor, roughness, metallic);
    } else if (mat.y == MAT_PILLAR) {
        pillarProperty(p, normalVec, mat, baseColor, roughness, metallic);
    } else if (mat.y == MAT_BOARD) {
        boardProperty(p, mat, baseColor, roughness, metallic);
    } else if (mat.y == MAT_LIGHT) {
        baseColor = vec3(50.0);
    } else if (mat.y == MAT_GOLD) {
        baseColor = vec3(0.8, 0.7, 0.1);
        roughness = 0.3;
        metallic = 1.0;
    } else if (mat.y == MAT_BLACK) {
        baseColor = vec3(0.2);
        roughness = 0.1;
        metallic = 0.0;
    } else if (mat.y == MAT_SPHERE) {
        float intensity = smoothstep(13.0, 15.0, time) * 5.0 + 1.0;
        if (time > 17.0 && time < 47.75) {
            intensity = exp(-fract(time*0.5) * 6.0) * 40.0 + 0.8;
        } else if (time > 47.75 && time < 49.0) {
            intensity = 40.0;
        }
        baseColor = vec3(1.0) * intensity;
        roughness = 0.0;
        metallic = 0.0;
    }
}

void raymarch(vec3 origin, vec3 ray, int iter, out vec3 pos, out vec2 mat, out float t) {
    t = 0.01;
    vec3 col = vec3(0.0);
    int i;
    for(i=0; i<iter; i++) {
        pos = origin + ray * t;
        mat = map(pos);
        if (mat.x < 0.001) {
            break;
        }
        t += mat.x;
    }
}

vec3 shade(vec3 p, vec3 baseColor, float t, float metallic) {
    p.x = abs(p.x) - 1.0;
    float attenuation = 1.0 / (pow(abs(p.x), 2.0) + pow(abs(p.y + 1.5), 2.) + 6.0);
    vec3 col = baseColor * vec3(5.0) * attenuation;
    col = mix( col, vec3(0.05, 0.02, 0.005), 1.0-exp(-t*0.02) );
    return col;
}

vec3 fresnelSchlick_roughness(vec3 F0, float cosTheta, float roughness) {
    return F0 + (max(vec3(1.0 - roughness), F0) - F0) * pow(1.0 - cosTheta, 5.0);
}

vec3 shadeWithReflect(vec3 p, vec3 ray, vec2 uv, vec3 baseColor, float t, vec3 n, float metallic) {
    p.x = abs(p.x) - 1.0;
    float attenuation = 1.0 / (pow(abs(p.x), 2.0) + pow(abs(p.y + 1.5), 2.) + 6.0);
    
    float roughness = textureLod(roughnessTexture, uv, 0.0).w;
	vec3 F0 = mix(vec3(0.04), baseColor, metallic);
    vec3 F  = fresnelSchlick_roughness(F0, max(dot(n, -ray), 0.0), roughness);
    vec3 kd = mix(vec3(1.0) - F, vec3(0.0), metallic);

    vec3 col = baseColor * vec3(5.0) * attenuation * kd;

    col += textureLod(reflectTexture, uv, 0.0).rgb * F;
    col = mix( col, vec3(0.05, 0.02, 0.005), 1.0-exp(-t*0.02) );
    return col;
}

void getCamera(vec2 p, out vec3 origin, out vec3 ray) {
    tt = time*0.9;
    if (time > 47.75 && time < 49.0) {
        tt = 47.0;
    }
    vec3 target = vec3(0.0);
    float f = 3.5;

    if (time < 9.0) {
        float z = tt * 0.5 + 2.0;
        origin = vec3(5.0, 1.0, z);
        target = vec3(0.0, -0.5, z * 1.5 + 2.0);
    } else if (time < 17.0) {
        float z = tt * 0.5;
        origin = vec3(0.0, 0.0, z);
        target = vec3(0.0, -0.5, z*1.5 + 1.0);
    } else if (time < 33.0) {
        float z = tt * 0.5;
        origin = vec3(sin(tt*0.2) * 7.0, cos(tt*0.08)*1., -5.0 + tt);
        target = vec3(0.0, -0.5, tt);
    } else if (time < 49.0) {
        float z = tt * 0.5;
        float ttt = tt + 0.1;
        origin = vec3(sin(ttt*0.2) * 7.0, cos(ttt*0.05)*1., -5.0 + tt);
        target = vec3(0.0, -0.5, tt);
    } else if (time < 65.0) {
        float z = tt * 0.5;
        origin = vec3(0.0, 0.0, -z);
        target = vec3(0.0, -0.5, -z * 0.8);
    }

    vec3 fo = normalize(target - origin);
    vec3 si = normalize(cross(vec3(0.0, 1.0, 0.0), fo));
    vec3 up = normalize(cross(fo, si));
    ray = normalize(fo * f + si * p.x + up * p.y);
}

</script>

<script id="GBuffer" type="x-shader/x-fragment">
void mainImage( out vec4 fragColor, out vec4 fragColor2, out vec4 fragColor3, out vec4 fragColor4, in vec2 fragCoord ) {
    vec2 p = (fragCoord.xy * 2.0 - resolution.xy) / min(resolution.x, resolution.y);
    vec2 uv = fragCoord.xy / resolution.xy;
        
    if (abs(p.y) > 0.75) {
        return;
    }
    vec3 origin, ray;
    getCamera(p, origin, ray);

    vec3 col, pos;
    vec2 d;
    float t, roughness, metallic;
    vec3 baseColor, n;
    raymarch(origin, ray, 250, pos, d, t);
    materialToProperty(pos, d, baseColor, n, roughness, metallic);

    fragColor  = vec4(n, t);
    fragColor2 = vec4(d, metallic, roughness);
    fragColor3 = vec4(baseColor, 1.0);

    vec3 r = reflect(ray, n);
    raymarch(pos + n*0.1, r, 100, pos, d, t);
    materialToProperty(pos, d, baseColor, n, roughness, metallic);
    vec3 reflectCol = shade(pos, baseColor, t, metallic);
    fragColor4 = vec4(reflectCol, 1.0);
}

void main( )
{
    vec4 col, col2, col3, col4;
    mainImage(col, col2, col3, col4, gl_FragCoord.xy);
    fragColor  = col;
    fragColor2 = col2;
    fragColor3  = col3;
    fragColor4 = col4;
}
</script>

<script id="shade" type="x-shader/x-fragment">
void mainImage( out vec4 fragColor, in vec2 fragCoord ) {
    vec2 p = (fragCoord.xy * 2.0 - resolution.xy) / min(resolution.x, resolution.y);
    vec2 uv = fragCoord.xy / resolution.xy;
        
    if (abs(p.y) > 0.75) {
        return;
    }
    vec3 origin, ray;
    getCamera(p, origin, ray);

    vec3 baseColor = textureLod(baseColorTexture, uv, 0.0).rgb;
    vec4 depthNormal = textureLod(depthNormalTexture, uv, 0.0);
    vec4 matRoughness = textureLod(roughnessTexture, uv, 0.0);
    vec3 n = depthNormal.rgb;
    float t = depthNormal.w, roughness = matRoughness.w, metallic = matRoughness.z;
    vec2 d = matRoughness.xy;
    vec3 pos = origin + ray * t;

    vec3 primaryCol = shadeWithReflect(pos, ray, uv, baseColor, t, n, metallic);
    fragColor = vec4(primaryCol,1.0);
}

void main( )
{
    vec4 col;
    mainImage(col, gl_FragCoord.xy);
    fragColor  = col;
}
</script>

<script id="combineBloomAndFilters" type="x-shader/x-fragment">
#version 300 es
precision highp float;
uniform vec2  resolution;
uniform sampler2D tex;
uniform sampler2D bloomTex;
out vec4 fragColor;

float luminance(vec3 col)
{
    return dot(vec3(0.298912, 0.586611, 0.114478), col);
}

vec3 saturation(vec3 col, float scale)
{
    return mix(vec3(luminance(col)), col, scale);
}

vec3 contrast(vec3 col, float scale)
{
    return (col - 0.5) * scale + 0.5;
}

vec3 colorCorrect(vec3 col) {
    col = saturation(col, 0.5);
    col = pow(col, vec3(1.5));
    col = col * vec3(0.6, 0.9, 1.0);
    return col;
}

vec3 acesFilm(const vec3 x) {
    const float a = 2.51;
    const float b = 0.03;
    const float c = 2.43;
    const float d = 0.59;
    const float e = 0.14;
    return clamp((x * (a * x + b)) / (x * (c * x + d ) + e), 0.0, 1.0);
}

// https://www.shadertoy.com/view/MdfBRX
float Bokeh(vec2 p, vec2 sp, float size, float mi, float blur)
{
    float d = length(p - sp);
    float c = smoothstep(size, size*(1.-blur), d);
    c *= mix(mi, 1., smoothstep(size*.8, size, d));
    return c;
}

vec2 hash( vec2 p ){
    p = vec2( dot(p,vec2(127.1,311.7)),dot(p,vec2(269.5,183.3)));
    return fract(sin(p)*43758.5453) * 2.0 - 1.0;
}

vec3 dirt(vec2 uv, float n)
{
    vec2 p = fract(uv * n);
    vec2 st = (floor(uv * n) + 0.5) / n;
    vec2 rnd = hash(st);
    float c = Bokeh(p, vec2(0.5, 0.5) + vec2(0.3) * rnd, 0.2, abs(rnd.y * 0.4) + 0.3, 0.25 + rnd.x * rnd.y * 0.2);
    
    return vec3(c) * exp(rnd.x * 4.0);
}

vec3 dirtTexture(vec2 uv) {
    uv *= 2.5;
    vec3 di = vec3(0.0);
    di += dirt(uv - vec2(0.17), 60.0);
    di += dirt(uv - vec2(0.17), 30.0);
    di += dirt(uv - vec2(0.41), 100.75);
    di += dirt(uv - vec2(0.41), 20.75);
    return di * 0.01;
}

void main( )
{
    vec2 p = (gl_FragCoord.xy*2.0-resolution)/resolution.y; 
    vec2 uv = gl_FragCoord.xy / resolution;
    vec3 col = vec3(0.0);

    if (abs(p.y) > 0.75) {
        fragColor=vec4(col,1);
        return;
    }

    vec3 bloom = textureLod(bloomTex, clamp(uv, 0.0, 1.0), 0.0).rgb;
    col = textureLod(tex, uv, 0.0).rgb + bloom;

    vec2 dirtuv = gl_FragCoord.xy / min(resolution.x, resolution.y);
    col +=  pow(bloom, vec3(1.5)) * dirtTexture(dirtuv);

    col = colorCorrect(col);
    col = acesFilm(col*0.3);
    col = pow(col, vec3(1.0/2.2));

    vec3 sclr = smoothstep(0.0, 1.0, col.rgb);

    col.r = mix(col.r, sclr.r, 1.0);
    col.g = mix(col.g, sclr.g, 0.8);
    col.b = mix(col.b, sclr.b, 0.6);

    fragColor=vec4(col,1);
}
</script>

<script id="postProcess" type="x-shader/x-fragment">
#version 300 es
precision highp float;
uniform vec2  resolution;
uniform sampler2D tex;
uniform float time;
out vec4 fragColor;

vec4[4] blackOutParam = vec4[](vec4(0.,4.,16.,16.1),vec4(17.,17.1,32.,32.1),vec4(33.,33.1,48.0,48.01),vec4(49.,49.1,61.,65.));

float blackOutTime() {
    float t = time;
    int id = int(t>17.)+int(t>33.)+int(t>49.);
    vec4 p = blackOutParam[id];
    return smoothstep(p.x, p.y, t) - smoothstep(p.z, p.w, t);
}

float hash( float n )
{
    return fract(sin(n)*43758.5453);
}

vec2 disp(vec2 p, float n, float s) {
    p.y += (hash(floor(time*s) + n) * 2.0 - 1.0);
    return vec2(smoothstep(0.0011, 0.001, abs(abs(p.y) - vec2(0.5,0.1)) - vec2(0.25,0.02)));
}

void main( )
{
    vec2 p = (gl_FragCoord.xy*2.0-resolution)/resolution.y; 
    vec3 col = vec3(0.0);

    p = (gl_FragCoord.xy*2.0-resolution)/resolution.xy;

    float glitchPower = 0.0;
    if (time > 47.75 && time < 49.) {
        glitchPower = 1.0;
    }

    vec2 d = (disp(p*0.8, 0.0, 10.) * 0.4 + disp(p*1.6, 100., 20.0) * 0.2) * glitchPower;

    vec2 uvR = (p+p*dot(p, p)*0.01 + d*1.0) * 0.5 + 0.5;
    vec2 uvG = (p+p*dot(p, p)*0.02 + d*1.2) * 0.5 + 0.5;
    vec2 uvB = (p+p*dot(p, p)*0.03 + d*1.4) * 0.5 + 0.5;

    float r = textureLod(tex, uvR, 0.0).r;
    float g = textureLod(tex, uvG, 0.0).g;
    float b = textureLod(tex, uvB, 0.0).b;

    col = vec3(r,g,b);
    p = gl_FragCoord.xy / resolution;
    p *=  1.0 - p.yx;
    float vig = p.x*p.y * 30.0;
    vig = clamp(pow(vig, 0.5), 0.0, 1.0);    
    col *= vig;
    col *= blackOutTime();
    fragColor=vec4(col,1);
}
</script>

<link rel="stylesheet" href="./style.css" />
<script type="text/javascript" src="./utility.js"></script>
<script type="text/javascript" src="./app.js"></script>

<html>
    <body>

    </body>
</html>